
# Tools library
SET(OWN_LIB_NAME ORAIFTools)

SET(${OWN_LIB_NAME}_HDRS
  oraFileTools.h
  oraStringTools.h
  oraIniAccess.h
  SimpleDebugger.h
  oraSimpleMacros.h
  oraMultiOutputStreamBuffer.h
  oraGenericTextFormatter.h
)

SET(${OWN_LIB_NAME}_SRCS
  oraFileTools.cxx
  oraStringTools.cxx
  oraIniAccess.cxx
  SimpleDebugger.cxx
  oraMultiOutputStreamBuffer.cxx
  oraGenericTextFormatter.cxx
)

IF(ORAIFUTILS_USE_QT)
  SET(${OWN_LIB_NAME}_HDRS ${${OWN_LIB_NAME}_HDRS}
    oraQFileTools.h
    oraQStringTools.h
  )
  
  SET(${OWN_LIB_NAME}_SRCS ${${OWN_LIB_NAME}_SRCS}
    oraQFileTools.cxx
    oraQStringTools.cxx
  )
ENDIF(ORAIFUTILS_USE_QT)

ADD_LIBRARY(${OWN_LIB_NAME} ${${OWN_LIB_NAME}_SRCS} ${${OWN_LIB_NAME}_HDRS})
SET_TARGET_PROPERTIES(${OWN_LIB_NAME} PROPERTIES LINKER_LANGUAGE CXX)

SET(${OWN_LIB_NAME}_LINK_LIBRARIES ITKCommon)
IF(ORAIFUTILS_USE_QT)
  SET(${OWN_LIB_NAME}_LINK_LIBRARIES ${${OWN_LIB_NAME}_LINK_LIBRARIES} ${QT_QTCORE_LIBRARY})
ENDIF(ORAIFUTILS_USE_QT)
TARGET_LINK_LIBRARIES(${OWN_LIB_NAME} ${${OWN_LIB_NAME}_LINK_LIBRARIES})

# Add headers to export-set. NOTE: Quotes are important here otherwise only the first header is installed!
SET_TARGET_PROPERTIES(${OWN_LIB_NAME} PROPERTIES PUBLIC_HEADER "${${OWN_LIB_NAME}_HDRS}")

INSTALL(TARGETS ${OWN_LIB_NAME}
  # Add the library to the export-set
  EXPORT ORAIFUTILSLibraryDepends
  RUNTIME DESTINATION "${INSTALL_BIN_DIR}" COMPONENT bin
  LIBRARY DESTINATION "${INSTALL_LIB_DIR}" COMPONENT lib
  ARCHIVE DESTINATION "${INSTALL_LIB_DIR}" COMPONENT lib/static
  PUBLIC_HEADER DESTINATION "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_TOOLS_SUBDIR}"
  COMPONENT dev)

# UNIT TEST
IF (ORAIFUTILS_BUILD_TESTING)
  ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/testing)
ENDIF (ORAIFUTILS_BUILD_TESTING)

